OBJECT Codeunit 50026 DotNet_Word.ParagraphProp
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DotNetParagraphProperties@1000 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.ParagraphProperties";

    [External]
    PROCEDURE Create@1();
    BEGIN
      DotNetParagraphProperties := DotNetParagraphProperties.ParagraphProperties;
    END;

    [External]
    PROCEDURE CreateFromOuterXml@2(OuterXml@1000 : Text);
    BEGIN
      DotNetParagraphProperties := DotNetParagraphProperties.ParagraphProperties(OuterXml);
    END;

    [External]
    PROCEDURE GetOuterXml@3() : Text;
    BEGIN
      EXIT(DotNetParagraphProperties.OuterXml);
    END;

    [External]
    PROCEDURE GetInnerText@4() : Text;
    BEGIN
      EXIT(DotNetParagraphProperties.InnerText);
    END;

    [External]
    PROCEDURE SetJustification@5(Justification@1000 : 'Left,Start,Center,Right,End,Both,MediumKashida,Distribute,NumTab,HighKashida,LowKashida,ThaiDistribute');
    VAR
      DotNetJustification@1001 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.Justification";
      DotNetJustificationValues@1002 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.JustificationValues";
      DotNetEnumValue@1003 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.EnumValue`1";
      OpenXmlDotNetHelper@1004 : Codeunit 50067;
    BEGIN
      DotNetJustification := DotNetJustification.Justification;
      //We need this to deal with .NET Generics:
      OpenXmlDotNetHelper.CreateEnumValueGeneric(GETDOTNETTYPE(DotNetJustificationValues), Justification, DotNetEnumValue);
      DotNetJustification.Val := DotNetEnumValue;
      DotNetParagraphProperties.Justification := DotNetJustification;
    END;

    [External]
    PROCEDURE SetSpacingBetweenLines@6(VAR WordSpacingBetweenLines@1000 : Codeunit 50030);
    VAR
      DotNetSpacingBetweenLines@1001 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.SpacingBetweenLines";
    BEGIN
      WordSpacingBetweenLines.GetSpacingBetweenLines(DotNetSpacingBetweenLines);
      DotNetParagraphProperties.SpacingBetweenLines := DotNetSpacingBetweenLines;
    END;

    [External]
    PROCEDURE SetIndentation@7(VAR WordIndentation@1000 : Codeunit 50031);
    VAR
      DotNetIndentation@1001 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.Indentation";
    BEGIN
      WordIndentation.GetIndentation(DotNetIndentation);
      DotNetParagraphProperties.Indentation := DotNetIndentation;
    END;

    [External]
    PROCEDURE SetNumberingProperties@8(VAR WordNumberingProperties@1000 : Codeunit 50032);
    VAR
      DotNetNumberingProperties@1001 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.NumberingProperties";
    BEGIN
      WordNumberingProperties.GetNumberingProperties(DotNetNumberingProperties);
      DotNetParagraphProperties.NumberingProperties := DotNetNumberingProperties;
    END;

    [External]
    PROCEDURE SetParagraphStyleId@9(NewParagraphStyleId@1000 : Text);
    VAR
      DotNetParagraphStyleId@1001 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.ParagraphStyleId";
      DotNetStringValue@1002 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.StringValue";
    BEGIN
      DotNetParagraphStyleId := DotNetParagraphStyleId.ParagraphStyleId;
      DotNetParagraphStyleId.Val := DotNetStringValue.FromString(NewParagraphStyleId);
      DotNetParagraphProperties.ParagraphStyleId := DotNetParagraphStyleId;
    END;

    PROCEDURE SetParagraphProperties@10(NewDotNetParagraphProperties@1000 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.ParagraphProperties");
    BEGIN
      DotNetParagraphProperties := NewDotNetParagraphProperties;
    END;

    PROCEDURE GetParagraphProperties@11(VAR CurrentDotNetParagraphProperties@1000 : DotNet "'DocumentFormat.OpenXml, Version=2.0.5022.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.DocumentFormat.OpenXml.Wordprocessing.ParagraphProperties");
    BEGIN
      CurrentDotNetParagraphProperties := DotNetParagraphProperties;
    END;

    BEGIN
    END.
  }
}

