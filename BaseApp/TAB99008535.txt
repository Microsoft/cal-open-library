OBJECT Table 99008535 TempBlob
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=;
  }
  PROPERTIES
  {
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Integer        }
    { 2   ;   ;Blob                ;BLOB           }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GlobalInStream@1000 : InStream;
      GlobalOutStream@1012 : OutStream;
      ReadLinesInitialized@1001 : Boolean;
      WriteLinesInitialized@1013 : Boolean;
      NoContentErr@1002 : TextConst 'ENU=The BLOB field is empty.';
      UnknownImageTypeErr@1003 : TextConst 'ENU=Unknown image type.';
      XmlCannotBeLoadedErr@1004 : TextConst 'ENU=The XML cannot be loaded.';

    [External]
    PROCEDURE WriteAsText@3(Content@1001 : Text;Encoding@1000 : TextEncoding);
    VAR
      OutStr@1002 : OutStream;
    BEGIN
      CLEAR(Blob);
      IF Content = '' THEN
        EXIT;
      Blob.CREATEOUTSTREAM(OutStr,Encoding);
      OutStr.WRITETEXT(Content);
    END;

    [External]
    PROCEDURE ReadAsText@5(LineSeparator@1001 : Text;Encoding@1000 : TextEncoding) Content : Text;
    VAR
      InStream@1002 : InStream;
      ContentLine@1003 : Text;
    BEGIN
      Blob.CREATEINSTREAM(InStream,Encoding);

      InStream.READTEXT(Content);
      WHILE NOT InStream.EOS DO BEGIN
        InStream.READTEXT(ContentLine);
        Content += LineSeparator + ContentLine;
      END;
    END;

    [External]
    PROCEDURE StartReadingTextLines@8(Encoding@1000 : TextEncoding);
    BEGIN
      Blob.CREATEINSTREAM(GlobalInStream,Encoding);
      ReadLinesInitialized := TRUE;
    END;

    [External]
    PROCEDURE StartWritingTextLines@13(Encoding@1000 : TextEncoding);
    BEGIN
      CLEAR(Blob);
      Blob.CREATEOUTSTREAM(GlobalOutStream,Encoding);
      WriteLinesInitialized := TRUE;
    END;

    [External]
    PROCEDURE MoreTextLines@9() : Boolean;
    BEGIN
      IF NOT ReadLinesInitialized THEN
        StartReadingTextLines(TEXTENCODING::Windows);
      EXIT(NOT GlobalInStream.EOS);
    END;

    [External]
    PROCEDURE ReadTextLine@7() : Text;
    VAR
      ContentLine@1003 : Text;
    BEGIN
      IF NOT MoreTextLines THEN
        EXIT('');
      GlobalInStream.READTEXT(ContentLine);
      EXIT(ContentLine);
    END;

    [External]
    PROCEDURE WriteTextLine@14(Content@1000 : Text);
    BEGIN
      IF NOT WriteLinesInitialized THEN
        StartWritingTextLines(TEXTENCODING::Windows);
      GlobalOutStream.WRITETEXT(Content);
    END;

    [External]
    PROCEDURE ToBase64String@1() : Text;
    VAR
      IStream@1000 : InStream;
      Convert@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      MemoryStream@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      Base64String@1001 : Text;
    BEGIN
      IF NOT Blob.HASVALUE THEN
        EXIT('');
      Blob.CREATEINSTREAM(IStream);
      MemoryStream := MemoryStream.MemoryStream;
      COPYSTREAM(MemoryStream,IStream);
      Base64String := Convert.ToBase64String(MemoryStream.ToArray);
      MemoryStream.Close;
      EXIT(Base64String);
    END;

    [External]
    PROCEDURE FromBase64String@2(Base64String@1000 : Text);
    VAR
      OStream@1001 : OutStream;
      Convert@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      MemoryStream@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
    BEGIN
      IF Base64String = '' THEN
        EXIT;
      MemoryStream := MemoryStream.MemoryStream(Convert.FromBase64String(Base64String));
      Blob.CREATEOUTSTREAM(OStream);
      MemoryStream.WriteTo(OStream);
      MemoryStream.Close;
    END;

    [External]
    PROCEDURE GetHTMLImgSrc@4() : Text;
    VAR
      ImageFormatAsTxt@1000 : Text;
    BEGIN
      IF NOT Blob.HASVALUE THEN
        EXIT('');
      IF NOT TryGetImageFormatAsTxt(ImageFormatAsTxt) THEN
        EXIT('');
      EXIT(STRSUBSTNO('data:image/%1;base64,%2',ImageFormatAsTxt,ToBase64String));
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGetImageFormatAsTxt@17(VAR ImageFormatAsTxt@1000 : Text);
    VAR
      Image@1002 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.Image";
      ImageFormatConverter@1004 : DotNet "'System.Drawing, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Drawing.ImageFormatConverter";
      InStream@1001 : InStream;
    BEGIN
      Blob.CREATEINSTREAM(InStream);
      Image := Image.FromStream(InStream);
      ImageFormatConverter := ImageFormatConverter.ImageFormatConverter;
      ImageFormatAsTxt := ImageFormatConverter.ConvertToString(Image.RawFormat);
    END;

    [External]
    PROCEDURE GetImageType@11() : Text;
    VAR
      ImageFormatAsTxt@1000 : Text;
    BEGIN
      IF NOT Blob.HASVALUE THEN
        ERROR(NoContentErr);
      IF NOT TryGetImageFormatAsTxt(ImageFormatAsTxt) THEN
        ERROR(UnknownImageTypeErr);
      EXIT(ImageFormatAsTxt);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGetXMLAsText@6(VAR Xml@1000 : Text);
    VAR
      XmlDoc@1002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      InStr@1001 : InStream;
    BEGIN
      Blob.CREATEINSTREAM(InStr);
      XmlDoc := XmlDoc.XmlDocument;
      XmlDoc.PreserveWhitespace := FALSE;
      XmlDoc.Load(InStr);
      Xml := XmlDoc.OuterXml;
    END;

    [External]
    PROCEDURE GetXMLAsText@19() : Text;
    VAR
      Xml@1000 : Text;
    BEGIN
      IF NOT Blob.HASVALUE THEN
        ERROR(NoContentErr);
      IF NOT TryGetXMLAsText(Xml) THEN
        ERROR(XmlCannotBeLoadedErr);
      EXIT(Xml);
    END;

    [External]
    PROCEDURE GetContentLength@20() : Integer;
    VAR
      MemoryStream@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      InStr@1000 : InStream;
    BEGIN
      Blob.CREATEINSTREAM(InStr);
      MemoryStream := MemoryStream.MemoryStream;
      COPYSTREAM(MemoryStream,InStr);
      EXIT(MemoryStream.ToArray.Length);
    END;

    [External]
    PROCEDURE GetContentHash@21(HashAlgorithmType@1002 : 'MD5,SHA1,SHA256,SHA384,SHA512') : Text;
    VAR
      MemoryStream@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      HashBytes@1005 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
      InStr@1003 : InStream;
    BEGIN
      Blob.CREATEINSTREAM(InStr);
      MemoryStream := MemoryStream.MemoryStream;
      COPYSTREAM(MemoryStream,InStr);
      IF NOT TryGenerateHash(MemoryStream.ToArray,HashBytes,FORMAT(HashAlgorithmType)) THEN
        ERROR(GETLASTERRORTEXT);

      EXIT(ConvertByteHashToString(HashBytes));
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGenerateHash@22(Bytes@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";VAR HashBytes@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";Algorithm@1002 : Text);
    VAR
      HashAlgorithm@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Security.Cryptography.HashAlgorithm";
      Encoding@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      HashAlgorithm := HashAlgorithm.Create(Algorithm);
      HashBytes := HashAlgorithm.ComputeHash(Bytes);
      HashAlgorithm.Dispose;
    END;

    LOCAL PROCEDURE ConvertByteHashToString@23(HashBytes@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array") : Text;
    VAR
      Byte@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Byte";
      StringBuilder@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.StringBuilder";
      I@1003 : Integer;
    BEGIN
      StringBuilder := StringBuilder.StringBuilder;
      FOR I := 0 TO HashBytes.Length - 1 DO BEGIN
        Byte := HashBytes.GetValue(I);
        StringBuilder.Append(Byte.ToString('X2'));
      END;
      EXIT(StringBuilder.ToString);
    END;

    BEGIN
    END.
  }
}

