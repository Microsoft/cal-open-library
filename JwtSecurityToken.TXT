OBJECT Codeunit 1290 SOAP Web Service Request Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=13.10.17;
    Time=13:26:29;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BodyPathTxt@1001 : TextConst '@@@={Locked};ENU=/soap:Envelope/soap:Body';
      ContentTypeTxt@1000 : TextConst '@@@={Locked};ENU="multipart/form-data; charset=utf-8"';
      FaultStringXmlPathTxt@1012 : TextConst '@@@={Locked};ENU=/soap:Envelope/soap:Body/soap:Fault/faultstring';
      NoRequestBodyErr@1015 : TextConst 'ENU=The request body is not set.';
      NoServiceAddressErr@1017 : TextConst 'ENU=The web service URI is not set.';
      ExpectedResponseNotReceivedErr@1009 : TextConst 'ENU=The expected data was not received from the web service.';
      SchemaNamespaceTxt@1007 : TextConst '@@@={Locked};ENU=http://www.w3.org/2001/XMLSchema';
      SchemaInstanceNamespaceTxt@1006 : TextConst '@@@={Locked};ENU=http://www.w3.org/2001/XMLSchema-instance';
      SecurityUtilityNamespaceTxt@1003 : TextConst '@@@={Locked};ENU=http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-utility-1.0.xsd';
      SecurityExtensionNamespaceTxt@1004 : TextConst '@@@={Locked};ENU=http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-secext-1.0.xsd';
      SoapNamespaceTxt@1002 : TextConst '@@@={Locked};ENU=http://schemas.xmlsoap.org/soap/envelope/';
      UsernameTokenNamepsaceTxt@1005 : TextConst '@@@={Locked};ENU=http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-username-token-profile-1.0#PasswordText';
      TempDebugLogTempBlob@1010 : TEMPORARY Record 99008535;
      ResponseBodyTempBlob@1020 : Record 99008535;
      ResponseInStreamTempBlob@1019 : Record 99008535;
      Trace@1016 : Codeunit 1292;
      GlobalRequestBodyInStream@1022 : InStream;
      HttpWebResponse@1021 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
      GlobalPassword@1013 : Text;
      GlobalURL@1014 : Text;
      GlobalUsername@1008 : Text;
      TraceLogEnabled@1011 : Boolean;
      GlobalTimeout@1024 : Integer;
      InternalErr@1028 : TextConst 'ENU=The remote service has returned the following error message:\\';
      GlobalSkipCheckHttps@1018 : Boolean;
      GlobalProgressDialogEnabled@1023 : Boolean;
      InvalidTokenFormatErr@1025 : TextConst 'ENU=The token must be in JWS or JWE Compact Serialization Format.';

    [TryFunction]
    [Internal]
    PROCEDURE SendRequestToWebService@17();
    VAR
      WebRequestHelper@1000 : Codeunit 1299;
      HttpWebRequest@1007 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";
      HttpStatusCode@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
      ResponseInStream@1006 : InStream;
    BEGIN
      CheckGlobals;
      BuildWebRequest(GlobalURL,HttpWebRequest);
      ResponseInStreamTempBlob.INIT;
      ResponseInStreamTempBlob.Blob.CREATEINSTREAM(ResponseInStream);
      CreateSoapRequest(HttpWebRequest.GetRequestStream,GlobalRequestBodyInStream,GlobalUsername,GlobalPassword);
      WebRequestHelper.GetWebResponse(HttpWebRequest,HttpWebResponse,ResponseInStream,
        HttpStatusCode,ResponseHeaders,GlobalProgressDialogEnabled);
      ExtractContentFromResponse(ResponseInStream,ResponseBodyTempBlob);
    END;

    LOCAL PROCEDURE BuildWebRequest@3(ServiceUrl@1000 : Text;VAR HttpWebRequest@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest");
    VAR
      DecompressionMethods@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.DecompressionMethods";
    BEGIN
      HttpWebRequest := HttpWebRequest.Create(ServiceUrl);
      HttpWebRequest.Method := 'POST';
      HttpWebRequest.KeepAlive := TRUE;
      HttpWebRequest.AllowAutoRedirect := TRUE;
      HttpWebRequest.UseDefaultCredentials := TRUE;
      HttpWebRequest.ContentType := ContentTypeTxt;
      IF GlobalTimeout <= 0 THEN
        GlobalTimeout := 600000;
      HttpWebRequest.Timeout := GlobalTimeout;
      HttpWebRequest.AutomaticDecompression := DecompressionMethods.GZip;
    END;

    LOCAL PROCEDURE CreateSoapRequest@2(RequestOutStream@1000 : OutStream;BodyContentInStream@1004 : InStream;Username@1003 : Text;Password@1005 : Text);
    VAR
      XmlDoc@1007 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      BodyXmlNode@1016 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      CreateEnvelope(XmlDoc,BodyXmlNode,Username,Password);
      AddBodyToEnvelope(BodyXmlNode,BodyContentInStream);
      XmlDoc.Save(RequestOutStream);
      TraceLogXmlDocToTempFile(XmlDoc,'FullRequest');
    END;

    LOCAL PROCEDURE CreateEnvelope@11(VAR XmlDoc@1011 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";VAR BodyXmlNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Username@1009 : Text;Password@1010 : Text);
    VAR
      XMLDOMMgt@1000 : Codeunit 6224;
      EnvelopeXmlNode@1007 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      HeaderXmlNode@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      SecurityXmlNode@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      UsernameTokenXmlNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TempXmlNode@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      PasswordXmlNode@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XmlDoc := XmlDoc.XmlDocument;
      WITH XMLDOMMgt DO BEGIN
        AddRootElementWithPrefix(XmlDoc,'Envelope','s',SoapNamespaceTxt,EnvelopeXmlNode);
        AddAttribute(EnvelopeXmlNode,'xmlns:u',SecurityUtilityNamespaceTxt);

        AddElementWithPrefix(EnvelopeXmlNode,'Header','','s',SoapNamespaceTxt,HeaderXmlNode);

        IF (Username <> '') OR (Password <> '') THEN BEGIN
          AddElementWithPrefix(HeaderXmlNode,'Security','','o',SecurityExtensionNamespaceTxt,SecurityXmlNode);
          AddAttributeWithPrefix(SecurityXmlNode,'mustUnderstand','s',SoapNamespaceTxt,'1');

          AddElementWithPrefix(SecurityXmlNode,'UsernameToken','','o',SecurityExtensionNamespaceTxt,UsernameTokenXmlNode);
          AddAttributeWithPrefix(UsernameTokenXmlNode,'Id','u',SecurityUtilityNamespaceTxt,CreateUUID);

          AddElementWithPrefix(UsernameTokenXmlNode,'Username',Username,'o',SecurityExtensionNamespaceTxt,TempXmlNode);
          AddElementWithPrefix(UsernameTokenXmlNode,'Password',Password,'o',SecurityExtensionNamespaceTxt,PasswordXmlNode);
          AddAttribute(PasswordXmlNode,'Type',UsernameTokenNamepsaceTxt);
        END;

        AddElementWithPrefix(EnvelopeXmlNode,'Body','','s',SoapNamespaceTxt,BodyXmlNode);
        AddAttribute(BodyXmlNode,'xmlns:xsi',SchemaInstanceNamespaceTxt);
        AddAttribute(BodyXmlNode,'xmlns:xsd',SchemaNamespaceTxt);
      END;
    END;

    LOCAL PROCEDURE CreateUUID@9() : Text;
    BEGIN
      EXIT('uuid-' + DELCHR(LOWERCASE(FORMAT(CREATEGUID)),'=','{}'));
    END;

    LOCAL PROCEDURE AddBodyToEnvelope@12(VAR BodyXmlNode@1005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";BodyInStream@1000 : InStream);
    VAR
      XMLDOMManagement@1001 : Codeunit 6224;
      BodyContentXmlDoc@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      XMLDOMManagement.LoadXMLDocumentFromInStream(BodyInStream,BodyContentXmlDoc);
      TraceLogXmlDocToTempFile(BodyContentXmlDoc,'RequestBodyContent');

      BodyXmlNode.AppendChild(BodyXmlNode.OwnerDocument.ImportNode(BodyContentXmlDoc.DocumentElement,TRUE));
    END;

    LOCAL PROCEDURE ExtractContentFromResponse@4(ResponseInStream@1000 : InStream;VAR BodyTempBlob@1002 : Record 99008535);
    VAR
      XMLDOMMgt@1005 : Codeunit 6224;
      ResponseBodyXMLDoc@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ResponseBodyXmlNode@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlNode@1008 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      BodyOutStream@1007 : OutStream;
      Found@1001 : Boolean;
    BEGIN
      TraceLogStreamToTempFile(ResponseInStream,'FullResponse',TempDebugLogTempBlob);
      XMLDOMMgt.LoadXMLNodeFromInStream(ResponseInStream,XmlNode);

      Found := XMLDOMMgt.FindNodeWithNamespace(XmlNode,BodyPathTxt,'soap',SoapNamespaceTxt,ResponseBodyXmlNode);
      IF NOT Found THEN
        ERROR(ExpectedResponseNotReceivedErr);

      ResponseBodyXMLDoc := ResponseBodyXMLDoc.XmlDocument;
      ResponseBodyXMLDoc.AppendChild(ResponseBodyXMLDoc.ImportNode(ResponseBodyXmlNode.FirstChild,TRUE));

      BodyTempBlob.Blob.CREATEOUTSTREAM(BodyOutStream);
      ResponseBodyXMLDoc.Save(BodyOutStream);
      TraceLogXmlDocToTempFile(ResponseBodyXMLDoc,'ResponseBodyContent');
    END;

    [Internal]
    PROCEDURE GetResponseContent@22(VAR ResponseBodyInStream@1000 : InStream);
    BEGIN
      ResponseBodyTempBlob.Blob.CREATEINSTREAM(ResponseBodyInStream);
    END;

    [Internal]
    PROCEDURE ProcessFaultResponse@15(SupportInfo@1001 : Text);
    VAR
      WebRequestHelper@1002 : Codeunit 1299;
      XMLDOMMgt@1006 : Codeunit 6224;
      WebException@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException";
      XmlNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ResponseInputStream@1000 : InStream;
      ErrorText@1009 : Text;
      ServiceURL@1010 : Text;
    BEGIN
      ErrorText := WebRequestHelper.GetWebResponseError(WebException,ServiceURL);

      IF ErrorText <> '' THEN
        ERROR(ErrorText);

      ResponseInputStream := WebException.Response.GetResponseStream;
      IF TraceLogEnabled THEN
        Trace.LogStreamToTempFile(ResponseInputStream,'WebExceptionResponse',TempDebugLogTempBlob);

      XMLDOMMgt.LoadXMLNodeFromInStream(ResponseInputStream,XmlNode);

      ErrorText := XMLDOMMgt.FindNodeTextWithNamespace(XmlNode,FaultStringXmlPathTxt,'soap',SoapNamespaceTxt);
      IF ErrorText = '' THEN
        ErrorText := WebException.Message;
      ErrorText := InternalErr + ErrorText + ServiceURL;

      IF SupportInfo <> '' THEN
        ErrorText += '\\' + SupportInfo;

      ERROR(ErrorText);
    END;

    [External]
    PROCEDURE SetGlobals@10(RequestBodyInStream@1000 : InStream;URL@1001 : Text;Username@1002 : Text;Password@1003 : Text);
    BEGIN
      GlobalRequestBodyInStream := RequestBodyInStream;

      GlobalSkipCheckHttps := FALSE;

      GlobalURL := URL;
      GlobalUsername := Username;
      GlobalPassword := Password;

      GlobalProgressDialogEnabled := TRUE;

      TraceLogEnabled := FALSE;
    END;

    [External]
    PROCEDURE SetTimeout@7(NewTimeout@1000 : Integer);
    BEGIN
      GlobalTimeout := NewTimeout;
    END;

    LOCAL PROCEDURE CheckGlobals@14();
    VAR
      WebRequestHelper@1000 : Codeunit 1299;
    BEGIN
      IF GlobalRequestBodyInStream.EOS THEN
        ERROR(NoRequestBodyErr);

      IF GlobalURL = '' THEN
        ERROR(NoServiceAddressErr);

      IF GlobalSkipCheckHttps THEN
        WebRequestHelper.IsValidUri(GlobalURL)
      ELSE
        WebRequestHelper.IsSecureHttpUrl(GlobalURL);
    END;

    LOCAL PROCEDURE TraceLogStreamToTempFile@33(VAR ToLogInStream@1000 : InStream;Name@1005 : Text;VAR TraceLogTempBlob@1001 : Record 99008535);
    BEGIN
      IF TraceLogEnabled THEN
        Trace.LogStreamToTempFile(ToLogInStream,Name,TraceLogTempBlob);
    END;

    LOCAL PROCEDURE TraceLogXmlDocToTempFile@67(VAR XmlDoc@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";Name@1005 : Text);
    BEGIN
      IF TraceLogEnabled THEN
        Trace.LogXmlDocToTempFile(XmlDoc,Name);
    END;

    [External]
    PROCEDURE SetTraceMode@1(NewTraceMode@1000 : Boolean);
    BEGIN
      TraceLogEnabled := NewTraceMode;
    END;

    [External]
    PROCEDURE DisableHttpsCheck@13();
    BEGIN
      GlobalSkipCheckHttps := TRUE;
    END;

    [External]
    PROCEDURE DisableProgressDialog@18();
    BEGIN
      GlobalProgressDialogEnabled := FALSE;
    END;

    [External]
    PROCEDURE HasJWTExpired@5(JsonWebToken@1000 : Text) : Boolean;
    VAR
      WebTokenAsJson@1001 : Text;
    BEGIN
      IF JsonWebToken = '' THEN
        EXIT(TRUE);
      IF NOT GetTokenDetailsAsJson(JsonWebToken,WebTokenAsJson) THEN
        ERROR(InvalidTokenFormatErr);
      EXIT(GetTokenDateTimeValue(WebTokenAsJson,'exp') < CURRENTDATETIME);
    END;

    [External]
    PROCEDURE GetTokenValue@6(WebTokenAsJson@1000 : Text;ClaimType@1001 : Text) : Text;
    VAR
      JObject@1004 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      JSONMgt@1002 : Codeunit 5459;
      ClaimValue@1003 : Text;
    BEGIN
      WITH JSONMgt DO BEGIN
        InitializeObject(WebTokenAsJson);
        GetJSONObject(JObject);
        IF GetStringPropertyValueFromJObjectByName(JObject,ClaimType,ClaimValue) THEN
          EXIT(ClaimValue);
      END;
    END;

    [External]
    PROCEDURE GetTokenDateTimeValue@8(WebTokenAsJson@1001 : Text;ClaimType@1000 : Text) : DateTime;
    VAR
      TypeHelper@1003 : Codeunit 10;
      Timestamp@1002 : Decimal;
    BEGIN
      IF NOT EVALUATE(Timestamp,GetTokenValue(WebTokenAsJson,ClaimType)) THEN
        EXIT;
      EXIT(TypeHelper.EvaluateUnixTimestamp(Timestamp));
    END;

    [TryFunction]
    [External]
    PROCEDURE GetTokenDetailsAsJson@19(JsonWebToken@1000 : Text;VAR WebTokenAsJson@1006 : Text);
    VAR
      JwtSecurityTokenHandler@1004 : DotNet "'System.IdentityModel.Tokens.Jwt, Version=5.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.System.IdentityModel.Tokens.Jwt.JwtSecurityTokenHandler";
      JwtSecurityToken@1003 : DotNet "'System.IdentityModel.Tokens.Jwt, Version=5.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.System.IdentityModel.Tokens.Jwt.JwtSecurityToken";
      Claim@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Security.Claims.Claim";
      JObject@1001 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      JSONMgt@1005 : Codeunit 5459;
    BEGIN
      IF JsonWebToken = '' THEN
        EXIT;

      JwtSecurityTokenHandler := JwtSecurityTokenHandler.JwtSecurityTokenHandler;
      JwtSecurityToken := JwtSecurityTokenHandler.ReadToken(JsonWebToken);

      WITH JSONMgt DO BEGIN
        InitializeEmptyObject;
        GetJSONObject(JObject);
        FOREACH Claim IN JwtSecurityToken.Claims DO BEGIN
          AddJPropertyToJObject(JObject,Claim.Type,Claim.Value);
        END;
      END;

      WebTokenAsJson := JObject.ToString;
    END;

    BEGIN
    END.
  }
}

OBJECT Codeunit 50060 JwtSecurityToken Test
{
  OBJECT-PROPERTIES
  {
    Date=13.10.17;
    Time=13:27:44;
    Version List=Navision.Guru,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1000 : Codeunit 130000;

    [Test]
    PROCEDURE TestJwtToken@1();
    VAR
      JwtMgt@1001 : Codeunit 1290;
      ExampleJwtToken@1010 : TextConst 'ENU=eyJ0eXAiOiJKV1QiLCJhbGciOiJIUzI1NiJ9.eyJhdWQiOiJodHRwOi8vd3d3LmV4YW1wbGUuY29tIiwiaXNzIjoic2VsZiIsIm5iZiI6MTM1Mzk3NDczNiwiZXhwIjoxMzUzOTc0ODU2LCJodHRwOi8vc2NoZW1hcy54bWxzb2FwLm9yZy93cy8yMDA1LzA1L2lkZW50aXR5L2NsYWltcy9uYW1lIjoiUGVkcm8iLCJodHRwOi8vc2NoZW1hcy5taWNyb3NvZnQuY29tL3dzLzIwMDgvMDYvaWRlbnRpdHkvY2xhaW1zL3JvbGUiOiJBdXRob3IifQ.a-Tu5ojQSyiGSzTb9E5QbEYxyhomywzh2wqKs4El7lc';
      UnableToParseJwtObject@1011 : TextConst 'ENU=Unable to parse Jwt object.  Missing JwtSecurityTokenHandler ?';
      WebTokenAsJson@1000 : Text;
      NotBefore@1002 : DateTime;
      Expires@1003 : DateTime;
      NotBeforeErr@1012 : TextConst 'ENU=Not Before timestamp not evaluated correctly';
      ExpiresErr@1013 : TextConst 'ENU=Expires timestamp not evaluated correctly';
      HasJWTExpiredErr@1100408000 : TextConst 'ENU=HasJWTExpired function returns incorrect results for token expire date';
    BEGIN
      IF JwtMgt.GetTokenDetailsAsJson(ExampleJwtToken,WebTokenAsJson) THEN BEGIN
        TestWebTokenAsJsonValues(WebTokenAsJson,'aud', 'http://www.example.com');
        TestWebTokenAsJsonValues(WebTokenAsJson,'iss', 'self');
        TestWebTokenAsJsonValues(WebTokenAsJson,'nbf', '1353974736');
        TestWebTokenAsJsonValues(WebTokenAsJson,'exp', '1353974856');
        TestWebTokenAsJsonValues(WebTokenAsJson,'http://schemas.xmlsoap.org/ws/2005/05/identity/claims/name', 'Pedro');
        TestWebTokenAsJsonValues(WebTokenAsJson,'http://schemas.microsoft.com/ws/2008/06/identity/claims/role', 'Author');

        NotBefore := JwtMgt.GetTokenDateTimeValue(WebTokenAsJson,'nbf');
        Expires := JwtMgt.GetTokenDateTimeValue(WebTokenAsJson,'exp');
        Assert.AreEqual(CREATEDATETIME(DMY2DATE(27,11,2012),000500T),NotBefore,NotBeforeErr);
        Assert.AreEqual(CREATEDATETIME(DMY2DATE(27,11,2012),000700T),Expires,ExpiresErr);
        Assert.AreEqual(Expires < CURRENTDATETIME,JwtMgt.HasJWTExpired(ExampleJwtToken),HasJWTExpiredErr);
      END ELSE
        ERROR(UnableToParseJwtObject);
    END;

    LOCAL PROCEDURE TestWebTokenAsJsonValues@2(WebTokenAsJson@1000 : Text;ClaimType@1001 : Text;ClaimValue@1002 : Variant);
    VAR
      ClaimTypeErr@1004 : TextConst 'ENU=Claim Type %1 has incorrect value.';
      JwtMgt@1003 : Codeunit 1290;
    BEGIN
      Assert.AreEqual(
        ClaimValue,
        JwtMgt.GetTokenValue(WebTokenAsJson,ClaimType),
        STRSUBSTNO(ClaimTypeErr,ClaimType)
      );
    END;

    BEGIN
    END.
  }
}

