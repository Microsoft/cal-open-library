OBJECT Codeunit 146041 Test DotNet_WebClient
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1000 : Codeunit 130000;
      UrlSecurityErr@1001 : TextConst 'ENU=Security settings prevents download of this file.';

    [Test]
    PROCEDURE TestSuccessfulhttpDownload@1();
    VAR
      TempBlob@1002 : Record 99008535;
      DotNet_WebClient@1000 : Codeunit 3041;
      DotNet_MemoryStream@1001 : Codeunit 3009;
      OutStream@1003 : OutStream;
      Url@1004 : Text;
    BEGIN
      // [GIVEN] A http Url
      Url := 'http://www.microsoft.com';

      // [WHEN] Downloaded with Web Client to Memory Stream
      DotNet_WebClient.Init;
      DotNet_WebClient.DownloadData(Url,DotNet_MemoryStream);

      // [THEN] Then data should be downloaded
      TempBlob.Blob.CREATEOUTSTREAM(OutStream);
      DotNet_MemoryStream.WriteTo(OutStream);
      Assert.AreEqual(TRUE,TempBlob.Blob.HASVALUE,'http Download failed!');
    END;

    [Test]
    PROCEDURE TestSuccessfulhttpsDownload@2();
    VAR
      TempBlob@1003 : Record 99008535;
      DotNet_WebClient@1002 : Codeunit 3041;
      DotNet_MemoryStream@1001 : Codeunit 3009;
      OutStream@1000 : OutStream;
      Url@1004 : Text;
    BEGIN
      // [GIVEN] A https Url
      Url := 'https://www.microsoft.com';

      // [WHEN] Downloaded with Web Client to Memory Stream
      DotNet_WebClient.Init;
      DotNet_WebClient.DownloadData(Url,DotNet_MemoryStream);

      // [THEN] Then data should be downloaded
      TempBlob.Blob.CREATEOUTSTREAM(OutStream);
      DotNet_MemoryStream.WriteTo(OutStream);
      Assert.AreEqual(TRUE,TempBlob.Blob.HASVALUE,'https Download failed!');
    END;

    [Test]
    PROCEDURE TestFailedFileSystemDownload@3();
    VAR
      DotNet_WebClient@1002 : Codeunit 3041;
      DotNet_MemoryStream@1001 : Codeunit 3009;
      FileManagement@1000 : Codeunit 419;
      Url@1003 : Text;
    BEGIN
      // [GIVEN] A Url
      Url := FileManagement.CombinePath(APPLICATIONPATH,'CustomSettings.config');

      // [WHEN] Downloaded with Web Client to Memory Stream
      DotNet_WebClient.Init;
      ASSERTERROR DotNet_WebClient.DownloadData(Url,DotNet_MemoryStream);

      // [THEN] Then data download should fail
      Assert.AreEqual(UrlSecurityErr,GETLASTERRORTEXT,'Incorrect error message');
    END;

    [Test]
    PROCEDURE TestSuccessfulhttpsDownloadWithAuthentication@7();
    VAR
      TempBlob@1003 : Record 99008535;
      DotNet_WebClient@1002 : Codeunit 3041;
      DotNet_MemoryStream@1001 : Codeunit 3009;
      DotNet_Credential@1005 : Codeunit 3029;
      OutStream@1000 : OutStream;
      Url@1004 : Text;
    BEGIN
      // [GIVEN] A https Url and authentication
      Url := 'https://httpbin.org/basic-auth/testuser/testpassword';
      DotNet_Credential.InitCredential('testuser','testpassword');

      // [WHEN] Downloaded with Web Client to Memory Stream
      DotNet_WebClient.Init;
      DotNet_WebClient.SetCredentials(DotNet_Credential);
      DotNet_WebClient.DownloadData(Url,DotNet_MemoryStream);

      // [THEN] Then data should be downloaded
      TempBlob.Blob.CREATEOUTSTREAM(OutStream);
      DotNet_MemoryStream.WriteTo(OutStream);
      Assert.AreEqual(TRUE,TempBlob.Blob.HASVALUE,'Basic authentication failed!');
    END;

    [Test]
    PROCEDURE TestSuccessfulhttpsDownloadViaTempBlob@5();
    VAR
      TempBlob@1003 : Record 99008535;
      Url@1004 : Text;
    BEGIN
      // [GIVEN] A https Url
      Url := 'https://www.microsoft.com';

      // [WHEN] Downloaded with Web Client to Memory Stream
      TempBlob.TryDownloadFromUrl(Url);

      // [THEN] Then data should be downloaded
      Assert.AreEqual(TRUE,TempBlob.Blob.HASVALUE,'https Download failed!');
    END;

    [Test]
    PROCEDURE TestFailedFileSystemDownloadViaTempBlob@4();
    VAR
      TempBlob@1001 : Record 99008535;
      FileManagement@1000 : Codeunit 419;
      Url@1003 : Text;
    BEGIN
      // [GIVEN] A Url
      Url := FileManagement.CombinePath(APPLICATIONPATH,'CustomSettings.config');

      // [WHEN] Downloaded with Web Client to Memory Stream
      ASSERTERROR TempBlob.TryDownloadFromUrl(Url);

      // [THEN] Then data download should fail
      Assert.AreEqual(UrlSecurityErr,GETLASTERRORTEXT,'Incorrect error message');
    END;

    BEGIN
    END.
  }
}

